" =============================================================================
" File: conversion.vim
" Description: Conversion from info docs (plain text) to Vim help format
" Author: Daniel Campoverde [alx741] <alx741@riseup.net>
" =============================================================================


setlocal nogdefault

" Info2help() {{{1
" Convert Info plain text files generated by ` info page > result `
" to a convenient vim help file format
function! vinfo#conversion#info2help()

    " Add vim modelines for help-file
    " tw=78 ts=8 ft=help norl
    norm! Go vim:tw=78:ts=8:ft=help:norl:

    " Convert node subtitles (replace = and . underlining with -)
    g/\v^$\n.+\n[=.]+\n^$\n/+2 s/[=.]/-/g

    " Convert node titles (replace * underlining with =)
    g/\v^$\n.+\n\*+\n^$\n/+2 s/\*/=/g

    " Convert Menu marks to vim help-files syntax
    %s/^* Menu:/MENU/e

    " Create tag references
    " Change blank spaces, '-' with '_' and apply tag notation with '|'
    g/\v^\*\s+(.+)::/exe "norm! Wvt:\<Esc>"|s/\%V[ -]/_/ge
    %s/\v^\*\s+(.+)::/\* |\1|::/e

    " Remove false tags
    g/\v\*[^\* ]+\*/ norm! f*xf*x

    " Create tags
    g/\v^File: /call s:Create_tag()

    " Mark Nodes separations
    let @o = "==============================================================================\n"
    g/\v^File: /norm! "oPj"op
endfunction
" }}}1



" Create_tag() {{{1
" Takes Node names, remove white spaces
" and add '*' tag notation
function! s:Create_tag()
    " Create self node tag
    /\vNode: \zs
    " / in scripts only moves to the line of the match, not to the match
    " because it is actually :/ and not plain /.  Therefore we use 0n to move
    " to the match.
    norm! 0n"oyt,mm
    let @o = "\n*" . @o . "*\n"
    put o
    +1
    s/[- ]/_/ge
    right
    norm! 'm

    " Create tag references
    " Node:
    /\vNode: \zs
    norm! 0nvt,y
    call s:Format_tag()
    " Next:
    /\vNext: \zs
    norm! 0nvt,y
    call s:Format_tag()
    " Prev:
    /\vPrev: \zs
    norm! 0nvt,y
    call s:Format_tag()
    " Up:
    /\vUp: \zs
    norm! 0nvg_y
    call s:Format_tag()
endfunction
" }}}1



" Format_Tag() {{{1
function! s:Format_tag()
    if @@ !~? '.\+|' && @@ !~? '.\+)'
        s/\%V[ -]/_/ge
        s/\%V.*\%V./|&|/
        norm! 'm
    endif
endfunction
" }}}1
